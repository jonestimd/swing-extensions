/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2023 Timothy D. Jones
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

plugins {
    id 'java'
    id 'jacoco'
    id 'maven-publish'
    id 'signing'
}

compileJava.sourceCompatibility = 1.8
compileJava.targetCompatibility = 1.8

group = 'io.github.jonestimd'
version = '1.4.5'

repositories {
    mavenCentral()
}

dependencies {
    implementation( group: 'com.google.guava', name: 'guava', version: '18.0' )

    testCompileOnly( group: 'junit', name: 'junit', version: '4.7' )
    testImplementation( group: 'org.assertj', name: 'assertj-swing-junit', version: '3.9.2' )
    testCompileOnly( group: 'org.mockito', name: 'mockito-core', version: '3.1.0' )

    testRuntimeOnly( group: 'com.jgoodies', name: 'jgoodies-looks', version: '2.5.3' )
    testRuntimeOnly( group: 'com.jgoodies', name: 'jgoodies-common', version: '1.7.0' )
}

jar {
    manifest {
        attributes 'Implementation-Title': 'io.github.jonestimd:swing-extensions',
                   'Implementation-Version': version
    }
}

jacocoTestReport {
    reports {
        xml.required = true
        csv.required = false
    }
}

check.dependsOn jacocoTestReport

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('link', 'http://docs.oracle.com/javase/8/docs/api')
    options.addStringOption('header',
            '<style>' +
            'div.description table, div.description td, div.description th ' +
                    '{ border: 1px solid black; border-collapse: collapse; padding: 2px 3px 0 3px}' +
            '</style>')
}

task javadocJar(type: Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    archiveClassifier = "sources"
    from(sourceSets.main.allSource)
}

java {
    withSourcesJar()
    withJavadocJar()
}

if (hasProperty('signing.keyId')) signing {
    sign configurations.archives
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            pom {
                name = rootProject.group + ':' + rootProject.name
                packaging = 'jar'
                description = 'Extensions for Java Swing'
                url = 'https://jonestimd.github.io/swing-extensions'

                scm {
                    connection = 'scm:git:https://github.com/jonestimd/swing-extensions.git'
                    developerConnection = 'scm:git:https://github.com/jonestimd/swing-extensions.git'
                    url = 'https://github.com/jonestimd/swing-extensions'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/jonestimd/swing-extensions/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        name = 'Tim Jones'
                        organizationUrl = 'https://github.com/jonestimd'
                        email = 'jonestimd@users.noreply.github.com'
                    }
                }
            }
        }
    }
    repositories {
        maven {
            name = 'sonatype'
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials {
                username = ossrhUsername
                password = ossrhPassword
            }
        }
    }
}

signing {
    if (project.hasProperty('ossrhUsername')) {
        sign publishing.publications.mavenJava
    }
}
