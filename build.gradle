/*
 * The MIT License (MIT)
 *
 * Copyright (c) 2021 Timothy D. Jones
 *
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 *
 * The above copyright notice and this permission notice shall be included in all
 * copies or substantial portions of the Software.
 *
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
 * SOFTWARE.
 */

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'maven'
apply plugin: 'signing'

compileJava.sourceCompatibility = 1.8
compileJava.targetCompatibility = 1.8

group = 'io.github.jonestimd'
version = '1.4.1'

repositories {
    mavenCentral()
}

dependencies {
    compile( group: 'com.google.guava', name: 'guava', version: '18.0' )

    testCompile( group: 'junit', name: 'junit', version: '4.7' )
    testCompile( group: 'org.assertj', name: 'assertj-swing-junit', version: '3.9.2' )
    testCompile( group: 'org.mockito', name: 'mockito-core', version: '3.1.0' )

    testRuntime( group: 'com.jgoodies', name: 'jgoodies-looks', version: '2.5.3' )
    testRuntime( group: 'com.jgoodies', name: 'jgoodies-common', version: '1.7.0' )
}

jar {
    manifest {
        attributes 'Implementation-Title': 'io.github.jonestimd:swing-extensions',
                   'Implementation-Version': version
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        csv.enabled false
    }
}

check.dependsOn jacocoTestReport

javadoc {
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('link', 'http://docs.oracle.com/javase/8/docs/api')
    options.addStringOption('header',
            '<style>' +
            'div.description table, div.description td, div.description th ' +
                    '{ border: 1px solid black; border-collapse: collapse; padding: 2px 3px 0 3px}' +
            '</style>')
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourceJar(type: Jar) {
    classifier "sources"
    from(sourceSets.main.allSource)
}

artifacts {
    archives javadocJar, sourceJar
}

if (hasProperty('signing.keyId')) signing {
    sign configurations.archives
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            if (project.hasProperty('ossrhUsername')) repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: 'file:///' + System.properties['user.home'] + '/.m2/repository')

            pom.project {
                name = rootProject.group + ':' + rootProject.name
                packaging = 'jar'
                description = 'Extensions for Java Swing'
                url = 'https://jonestimd.github.io/swing-extensions'

                scm {
                    connection = 'scm:git:https://github.com/jonestimd/swing-extensions.git'
                    developerConnection = 'scm:git:https://github.com/jonestimd/swing-extensions.git'
                    url = 'https://github.com/jonestimd/swing-extensions'
                }

                licenses {
                    license {
                        name = 'MIT License'
                        url = 'https://github.com/jonestimd/swing-extensions/blob/master/LICENSE'
                    }
                }

                developers {
                    developer {
                        name = 'Tim Jones'
                        organizationUrl = 'https://github.com/jonestimd'
                        email = 'jonestimd@users.noreply.github.com'
                    }
                }
            }
        }
    }
}